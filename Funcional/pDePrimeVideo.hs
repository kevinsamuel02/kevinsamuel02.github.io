data Serie = Serie {
    nombreSerie :: String,
    actores :: [Actor],
    presupuesto :: Int,
    temporadasEstimadas :: Int,
    rating :: Float,
    cancelada :: Bool
}

data Actor = Actor {
    nombreActor :: String,
    sueldo :: Int,
    restricciones :: [String]
}

-- PUNTO 1 

-- a. Saber si la serie estÃ¡ en rojo
--Si el presupuesto no alcanza a cubrir lo que quieren cobrar todos
estaEnRojo :: Serie -> Bool
estaEnRojo unaSerie = presupuesto unaSerie < totalSueldos unaSerie

totalSueldos :: Serie -> Int
totalSueldos = sum . map sueldo . actores


-- b. Saber si una serie es problemÃ¡tica
-- Ocurre si tienen mÃ¡s de 3 actores con mÃ¡s de 1 restricciÃ³n
esProblematica :: Serie -> Bool
esProblematica unaSerie = cantidadActoresComplicados unaSerie > 3


cantidadActoresComplicados :: Serie -> Int
cantidadActoresComplicados = length . filter tieneMuchasRestricciones . actores

tieneMuchasRestricciones :: Actor -> Bool
tieneMuchasRestricciones = (> 1) . length . restricciones

type Productor = Serie -> Serie

modificarActores :: ([Actor] -> [Actor]) -> Serie -> Serie
modificarActores f unaSerie = unaSerie { actores = f (actores unaSerie) }

modificarTemporadas :: (Int -> Int) -> Serie -> Serie
modificarTemporadas f unaSerie = unaSerie { temporadasEstimadas = f (temporadasEstimadas unaSerie) }

-- a. Con Favoritismos
-- Reemplaza los dos primeros actores por sus favoritos.
conFavoritismos :: [Actor] -> Productor
conFavoritismos favoritos = modificarActores (\listaActual -> favoritos ++ drop 2 listaActual)

-- b. Tim Burton
-- Es un caso particular de favoritismos con sus actores fetiche.
timBurton :: Productor
timBurton = conFavoritismos [johnnyDepp, helenaBonhamCarter]

johnnyDepp :: Actor
johnnyDepp = Actor "Johnny Depp" 20000000 []

helenaBonhamCarter :: Actor
helenaBonhamCarter = Actor "Helena Bonham Carter" 15000000 []

-- c. Gatopardeitor
-- No cambia nada.
gatopardeitor :: Productor
gatopardeitor = id

-- d. Estireitor
-- Duplica la cantidad de temporadas.
estireitor :: Productor
estireitor = modificarTemporadas (*2)

-- e. Desespereitor
-- Hace un combo de ideas (recibe una lista de productores y los aplica todos).
desespereitor :: [Productor] -> Productor
desespereitor productores = foldr (.) id productores

-- f. Canceleitor
-- Cancela si estÃ¡ en rojo o si el rating es bajo.
canceleitor :: Float -> Productor
canceleitor cifraRating unaSerie
    | estaEnRojo unaSerie || rating unaSerie < cifraRating = unaSerie { cancelada = True }
    | otherwise = unaSerie

{-3. Calcular el bienestar de una serie, en base a la sumatoria de estos conceptos:
- Si la serie tiene estimadas mÃ¡s de 4 temporadas, su bienestar es 5, de lo contrario
es (10 - cantidad de temporadas estimadas) * 2
- Si la serie tiene menos de 10 actores, su bienestar es 3, de lo contrario es (10 -
cantidad de actores que tienen restricciones), con un mÃ­nimo de 2
Aparte de lo mencionado arriba, si la serie estÃ¡ cancelada, su bienestar es 0 mÃ¡s
allÃ¡ de cÃ³mo diesen el bienestar por longitud y por reparto.-} 
bienestar :: Serie -> Int
bienestar unaSerie
    | cancelada unaSerie = 0
    | otherwise          = bienestarPorLongitud unaSerie + bienestarPorReparto unaSerie


bienestarPorLongitud :: Serie -> Int
bienestarPorLongitud unaSerie
    | temporadasEstimadas unaSerie > 4 = 5
    | otherwise                        = (10 - temporadasEstimadas unaSerie) * 2


bienestarPorReparto :: Serie -> Int
bienestarPorReparto unaSerie
    | length (actores unaSerie) < 10 = 3
    | otherwise                      = max 2 (10 - cantidadConRestricciones unaSerie)

cantidadConRestricciones :: Serie -> Int
cantidadConRestricciones = length . filter (not . null . restricciones) . actores


{-4. Dada una lista de series y una lista de productores, aplicar para cada serie el
productor que la haga mÃ¡s efectiva: es decir, el que le deja mÃ¡s bienestar.
-}

masEfectivas :: [Serie] -> [Productor] -> [Serie]
masEfectivas series productores = map (elegirMejorVersion productores) series


elegirMejorVersion :: [Productor] -> Serie -> Serie
elegirMejorVersion productores unaSerie = 
    foldl1 quedarseConLaMejor (versionesDeLaSerie productores unaSerie)


versionesDeLaSerie :: [Productor] -> Serie -> [Serie]
versionesDeLaSerie productores unaSerie = map (\productor -> productor unaSerie) productores

quedarseConLaMejor :: Serie -> Serie -> Serie
quedarseConLaMejor serie1 serie2
    | bienestar serie1 >= bienestar serie2 = serie1
    | otherwise                            = serie2

{-5 a. Â¿Se puede aplicar el productor gatopardeitor con una lista infinita de actores? (lo hizo gemini)
Respuesta: SÃ.

JustificaciÃ³n: El productor gatopardeitor es la funciÃ³n identidad (id). Esto significa que devuelve la serie exactamente como le llegÃ³, 
sin inspeccionar ni operar sobre sus componentes. Como Haskell trabaja con EvaluaciÃ³n Perezosa, no necesita recorrer la lista infinita
 de actores para devolver el objeto Serie. Simplemente pasa la referencia ("el puntero") de la estructura en memoria. Nada explota.-}

 {-b. Â¿Y a uno con favoritismos? Â¿De quÃ© depende?
Respuesta: SÃ, se puede aplicar, pero que el programa termine o se cuelgue depende de quÃ© hagamos despuÃ©s con el resultado.
JustificaciÃ³n de por quÃ© se puede aplicar: La funciÃ³n conFavoritismos hace esto: favoritos ++ drop 2 actores.
drop 2: En una lista infinita, simplemente avanza el puntero dos lugares. No necesita llegar al final.
++: Construye una nueva lista pegando los favoritos adelante. Haskell construye esta nueva lista de forma perezosa.
 El resultado es una nueva lista infinita que empieza con los favoritos. La operaciÃ³n de transformaciÃ³n es instantÃ¡nea y vÃ¡lida.
Â¿De quÃ© depende que funcione o no? (El "Consumer") Depende de la consulta que hagas sobre esa serie resultante:
Funciona âœ…: Si pedÃ­s nombre serie, take 10 (actores serie) o head (actores serie). Haskell solo evaluarÃ¡ lo necesario 
(los primeros elementos).
Se cuelga (Loop Infinito) ðŸ’€: Si intentÃ¡s calcular el bienestar.
Â¿Por quÃ©? Porque bienestar usa cantidadConRestricciones, que a su vez usa length.
Para saber la longitud de una lista infinita, Haskell tiene que recorrerla toda... y nunca termina.-}

{-6. Saber si una serie es controvertida, que es cuando no se cumple que cada actor de
la lista cobra mÃ¡s que el siguiente.-}

esControvertida :: Serie -> Bool
esControvertida unaSerie = not (cobraMasQueElSiguiente (actores unaSerie))


cobraMasQueElSiguiente :: [Actor] -> Bool
cobraMasQueElSiguiente listaActores = 
    all primeroGanaMas (armarParejas listaActores)

armarParejas :: [a] -> [(a,a)]
armarParejas lista = zip lista (tail lista)


primeroGanaMas :: (Actor, Actor) -> Bool
primeroGanaMas (uno, otro) = sueldo uno > sueldo otro

{-7. Explicar la inferencia del tipo de la siguiente funciÃ³n:
funcionLoca x y = filter (even.x) . map (length.y)

Respuesta final:

Haskell

funcionLoca :: (Int -> Int) -> (a -> [b]) -> [a] -> [Int]
(Nota tÃ©cnica: Si querÃ©s ser sÃºper preciso con Haskell, en lugar de [b] podrÃ­as poner Foldable t => t b, pero en un parcial [b] suele estar perfecto).

Resumen de la LÃ³gica para justificar:
map dicta la estructura intermedia: Como usamos length, la lista se transforma en [Int].

filter confirma la salida: Filtra esa lista de enteros, asÃ­ que devuelve [Int].

x conecta con el intermedio: Procesa los enteros que salen del length, asÃ­ que va de Int -> Int.

y conecta con la entrada: Procesa los elementos originales (a) y devuelve algo medible (lista).-}